# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

template("nsbox_image") {
  action(target_name) {
    pool = "//:console"

    forward_variables_from(invoker, ["deps"])

    image_root = rebase_path(invoker.image, root_build_dir)

    script = "//utils/nsbox-bender.py"
    outputs = ["$root_out_dir/images/$target_name.tar"]
    args = [
      "--force-color",
      "--export", rebase_path(outputs[0], root_build_dir),
      "--override-nsbox-version", release_version,
      "--override-nsbox-branch", release_branch,
    ]

    if (defined(invoker.tag) && invoker.tag != "") {
      args += ["$image_root:${invoker.tag}"]
    } else {
      args += [image_root]
    }

    sources = [
      "${invoker.image}/metadata.json",
      "${invoker.image}/playbook.yaml",
    ]

    if (defined(invoker.role_files)) {
      foreach(file, invoker.role_files) {
        sources += ["${invoker.image}/roles/$file"]
      }
    }

    if (defined(invoker.local) && invoker.local) {
      sources += ["${invoker.image}/Dockerfile"]
    }
  }
}

fedora_versions = ["30", "31"]

foreach(version, fedora_versions) {
  nsbox_image("fedora-$version-image") {
    image = "fedora"
    tag = version
    role_files = [
      "main/tasks/main.yaml",
      "main/templates/nsbox.repo",
    ]
  }
}

nsbox_image("arch-image") {
  image = "arch"
  local = true
  role_files = [
    "main/tasks/main.yaml",
  ]
}

group("images") {
  deps = [":arch-image"]
  foreach(version, fedora_versions) {
    deps += [":fedora-$version-image"]
  }
}
