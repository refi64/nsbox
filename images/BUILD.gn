# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

template("nsbox_image") {
  action(target_name) {
    pool = "//:console"

    forward_variables_from(invoker, ["deps"])

    image_root = rebase_path(invoker.image, root_build_dir)

    script = "//utils/nsbox-bender.py"
    outputs = ["$root_out_dir/images/$target_name.tar"]
    args = [
      "--force-color",
      "--export", rebase_path(outputs[0], root_build_dir),
      "--override-nsbox-version", release_version,
      "--override-nsbox-branch", release_branch,
      "--builder", image_builder,
    ]

    if (defined(invoker.tag) && invoker.tag != "") {
      args += ["$image_root:${invoker.tag}"]
    } else {
      args += [image_root]
    }

    sources = [
      "${invoker.image}/metadata.json",
      "${invoker.image}/playbook.yaml",
    ]

    if (defined(invoker.image_files)) {
      foreach(file, invoker.image_files) {
        sources += ["${invoker.image}/$file"]
      }
    }

    if (defined(invoker.local) && invoker.local) {
      sources += ["${invoker.image}/Dockerfile"]
    }
  }
}

images_with_tags = [
  {
    name = "fedora"
    versions = ["32", "33"]
    extra_image_files = [
      "roles/main/files/nsbox_trigger.py",
      "roles/main/files/nsbox-guest-tools.spec",
      "roles/main/tasks/build_guest_tools.yaml",
      "roles/main/vars/guest_tools.yaml",
    ]
  },
  {
    name = "debian"
    versions = ["buster"]
  },
]

foreach(spec, images_with_tags) {
  foreach(version, spec.versions) {
    nsbox_image("${spec.name}-$version-image") {
      image = spec.name
      tag = version
      image_files = [
        "roles/main/tasks/main.yaml",
      ]

      if (defined(spec.extra_image_files)) {
        image_files += spec.extra_image_files
      }
    }
  }
}

nsbox_image("arch-image") {
  image = "arch"
  local = true
  image_files = [
    "roles/main/files/nsbox-trigger.hook",
    "roles/main/files/PKGBUILD",
    "roles/main/tasks/build_guest_tools.yaml",
    "roles/main/tasks/main.yaml",
    "roles/main/vars/guest_tools.yaml",
  ]
}

group("images") {
  deps = [":arch-image"]
  foreach(spec, images_with_tags) {
    foreach(version, spec.versions) {
      deps += [":${spec.name}-$version-image"]
    }
  }
}
