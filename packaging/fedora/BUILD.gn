# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

import("//build/copy_target_outputs.gni")
import("//build/rpmbuild.gni")
import("//build/substitute_file.gni")
import("//build/symlink.gni")

action("archive") {
  outputs = ["$target_out_dir/nsbox-sources.tar"]
  depfile = "$target_gen_dir/nsbox-sources.d"

  script = "//build/source_archive.py"
  args = ["--source-root", rebase_path("//", root_build_dir),
          "--prefix", "$product_name-$release_version",
          "--out-tar", rebase_path(outputs[0], root_build_dir),
          "--out-dep", rebase_path(depfile, root_build_dir),
          "--include-vendor"]
}

substitute_file("nsbox.spec") {
  deps = ["//:release_files"]

  source = "nsbox.spec"

  vars = [
    ["PRODUCT_NAME", product_name],
    ["RDNS_NAME", rdns_name],
    ["VERSION", release_version],
    ["COMMIT", release_commit],
  ]
}

rpmbuild("rpm") {
  package_name = product_name

  version = release_version
  release = "1"
  has_debug = true

  extra_binary_packages = ["$product_name-bender"]
  extra_noarch_packages = ["$product_name-selinux"]

  if (!is_stable_build) {
    extra_binary_packages += ["nsbox-edge-alias", "nsbox-edge-bender-alias"]
  }

  archive_source = get_target_outputs(":archive")
  substituted_spec = get_target_outputs(":nsbox.spec")

  spec = substituted_spec[0]
  sources = [archive_source[0]]
  deps = [":archive", ":nsbox.spec"]
}

copy_target_outputs("install") {
  deps = [":rpm", ":nsbox.spec", ":archive"]
  outputs = ["$root_build_dir/rpm/{{source_file_part}}"]
}

group("fedora") {
  deps = [":install"]
}
