# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

install_dir = "$root_out_dir/install"

template("install_files") {
  copy(target_name) {
    if (defined(invoker.deps)) {
      deps = invoker.deps
    } else {
      deps = []
    }

    if (defined(invoker.sources)) {
      sources = invoker.sources
    } else {
      sources = []
    }

    if (defined(invoker.targets)) {
      foreach(target, invoker.targets) {
        sources += get_target_outputs(target)
      }

      deps += invoker.targets
    }

    if (defined(invoker.output_prefix)) {
      output_prefix = invoker.output_prefix
    } else {
      output_prefix = install_dir
    }

    outputs = ["$output_prefix/${invoker.output}"]
  }
}
