# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

if (target_os == "") {
  target_os = host_os
}

if (target_cpu == "") {
  target_cpu = host_cpu
}

if (current_cpu == "") {
  current_cpu = target_cpu
}

if (current_os == "") {
  current_os = target_os
}

declare_args() {
  # Mark this as a stable build.
  is_stable_build = false

  # The path to the Go binary.
  go_exe = "go"

  # The system's root installation prefix.
  prefix = "/usr"

  # The system's bin directory, relative to the system's prefix.
  bin_dir = "bin"

  # The system's libexec directory, relative to the system's prefix.
  libexec_dir = "libexec"

  # The system's data directory, relative to the system's prefix.
  share_dir = "share"

  # The system's shared state directory, which should be an absolute path.
  state_dir = "/var/lib"

  # The system's config directory, which should be an absolute path.
  config_dir = "/etc"

  # Build the Fedora package.
  fedora_package = false

  # Build the Fedora guest tools.
  fedora_guest_tools = false

  # The path to the rpmbuild executable.
  rpmbuild_exe = "rpmbuild"

  # The Fedora release to build RPMs for.
  fedora_rpm_target_release = "30"

  # The version of GN to use for the built packages.
  package_gn = "2f5276089c50cc76bc9282ec1246304c4dafc5b8"

  # Override the release number for build purposes.
  override_release_version = ""
}

# We're not building any C/C++ code, so the toolchain only needs to define a few tools.
set_default_toolchain("//build/minimal-toolchain")

# Define some common substitution vars that build files will use.
substitute_config_vars = [
  ["BIN_DIR", bin_dir],
  ["CONFIG_DIR", config_dir],
  ["LIBEXEC_DIR", libexec_dir],
  ["SHARE_DIR", share_dir],
  ["STATE_DIR", state_dir],
]

if (override_release_version != "") {
  release_version = override_release_version
} else {
  release_version = exec_script("//build/parse_release.py",
                                ["--head-file", rebase_path("//.git/logs/HEAD")],
                                "string",
                                ["//.git/logs/HEAD"])
}

if (is_stable_build) {
  release_branch = "stable"
  product_name = "nsbox"
} else {
  release_branch = "edge"
  product_name = "nsbox-edge"
}
