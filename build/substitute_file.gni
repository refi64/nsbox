# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

template("substitute_file") {
  action(target_name) {
    forward_variables_from(invoker, ["deps"])

    sources = [invoker.source]

    if (defined(invoker.output)) {
      outputs = [invoker.output]
    } else {
      outputs = ["$target_out_dir/$target_name"]
    }

    script = "//build/substitute_file.py"

    args = ["--source", rebase_path(invoker.source, root_build_dir),
            "--dest", rebase_path(outputs[0], root_build_dir)]

    if (defined(invoker.vars)) {
      foreach(var, invoker.vars) {
        args += ["--var", var[0], var[1]]
      }
    }

    if (defined(invoker.file_vars)) {
      foreach(file_var, invoker.file_vars) {
        args += ["--file-var", file_var[0], rebase_path(file_var[1], root_build_dir)]
      }
    }
  }
}
