# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

template("selinux_package") {
  action(target_name) {
    script = "//build/selinux.py"

    if (defined(invoker.name)) {
      name = invoker.name
    } else {
      name = target_name
    }

    forward_variables_from(invoker,
                           [
                             "sources",
                             "deps",
                           ])

    te = ""
    fc = ""
    foreach(source, sources) {
      ext = get_path_info(source, "extension")
      if (ext == "te") {
        assert(te == "")
        te = source
      } else if (ext == "fc") {
        assert(fc == "")
        fc = source
      }
    }

    assert(te != "")

    scratch_dir = "$target_gen_dir/$name-policy"
    outputs = [ "$target_out_dir/$name.pp.bz2" ]

    args = [
      "--make",
      selinux_make,
      "--makefile",
      selinux_makefile,
      "--variant",
      selinux_variant,
      "--out",
      rebase_path(outputs[0], root_build_dir),
      "--scratch-dir",
      rebase_path(scratch_dir, root_build_dir),
      "--te",
      rebase_path(te, root_build_dir),
      "--fc",
      rebase_path(fc, root_build_dir),
    ]
  }
}
