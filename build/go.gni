# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

template("go_binary") {
  action(target_name) {
    forward_variables_from(invoker, ["deps"])

    outputs = ["$target_out_dir/$target_name"]
    depfile = "$target_out_dir/$target_name.d"

    script = "//build/go.py"
    args = ["--go", go_exe, "--go-cache", rebase_path("$root_build_dir/gocache"),
            "--package", invoker.package,
            "--root", rebase_path(invoker.root, root_build_dir),
            "--out-bin", rebase_path(outputs[0], invoker.root),
            "--out-dep", rebase_path(depfile, invoker.root)]

    if (defined(invoker.static) && invoker.static) {
      args += ["--static"]
    }
  }
}
