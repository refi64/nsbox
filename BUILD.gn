# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

import("//build/go.gni")
import("//build/install.gni")
import("//build/python_binary.gni")
import("//build/substitute_file.gni")
import("//build/symlink.gni")

install_dir = "$root_out_dir/install"

pool("console") {
  depth = 1
}

action("release_files") {
  script = "//build/write_release.py"

  outputs = ["$root_out_dir/VERSION", "$root_out_dir/BRANCH"]
  args = ["--version", release_version, "--branch", release_branch,
          "--out-version", rebase_path(outputs[0], root_build_dir),
          "--out-branch", rebase_path(outputs[1], root_build_dir)]
}

# XXX: So, since we need to be able to write out files into what appears to be the source tree,
# but we can't actually (since GN does multiple build dirs, which is a feature that I would
# like to keep), then we need to instead copy ALL THE GO SOURCES over into the build tree.
# govendor is symlinked because that would mostly just be annoying to copy.

# Life is fun.

symlink("vendor_symlink") {
  name = "$go_target_dir/vendor"
  value = rebase_path("//vendor")
}

substitute_file("host_paths") {
  source = "internal/paths/host_paths.template.go"
  output = "$go_target_dir/internal/paths/host_paths.go"

  vars = substitute_config_vars + [
    ["PRODUCT_NAME", product_name]
  ]
}

substitute_file("nsbox_profile") {
  source = "//misc/profile.d-nsbox.sh"
  vars = substitute_config_vars
}

substitute_file("nsbox_policy") {
  source = "//misc/dev.nsbox.policy"
  output = "$target_out_dir/$rdns_name.policy"
  vars = [
    ["NSBOX_INVOKER", "$prefix/$libexec_dir/$product_name/nsbox-invoker"],
    ["RDNS_NAME", rdns_name],
  ]
}

substitute_file("nsbox_polkit_rules") {
  source = "//misc/dev.nsbox.rules"
  output = "$target_out_dir/$rdns_name.rules"
  vars = [
    ["RDNS_NAME", rdns_name],
  ]
}

copy("gofiles") {
  sources = [
    "go.mod",
    "go.sum",
    "cmd/nsbox/config.go",
    "cmd/nsbox/create.go",
    "cmd/nsbox/delete.go",
    "cmd/nsbox/info.go",
    "cmd/nsbox/images.go",
    "cmd/nsbox/kill.go",
    "cmd/nsbox/list.go",
    "cmd/nsbox/main.go",
    "cmd/nsbox/run.go",
    "cmd/nsbox/set_default.go",
    "cmd/nsbox/version.go",
    "cmd/nsboxd/main.go",
    "cmd/nsbox-invoker/main.go",
    "cmd/nsbox-host/enter.go",
    "cmd/nsbox-host/main.go",
    "cmd/nsbox-host/reload_exports.go",
    "cmd/nsbox-host/service.go",
    "cmd/nsbox-host/varlink_util.go",
    "internal/args/array.go",
    "internal/args/args.go",
    "internal/container/container.go",
    "internal/container/info.go",
    "internal/create/create.go",
    "internal/daemon/direct.go",
    "internal/daemon/transient.go",
    "internal/gtkicons/gtkicons.go",
    "internal/gtkicons/nsbox-gtkicons.c",
    "internal/gtkicons/nsbox-gtkicons.h",
    "internal/integration/xdgdesktop.go",
    "internal/image/image.go",
    "internal/inventory/inventory.go",
    "internal/kill/kill.go",
    "internal/log/log.go",
    "internal/nspawn/builder.go",
    "internal/paths/paths.go",
    "internal/ptyservice/client.go",
    "internal/ptyservice/service.go",
    "internal/release/release.go",
    "internal/session/enter.go",
    "internal/session/setup.go",
    "internal/userdata/check_privs.go",
    "internal/userdata/userdata.go",
    "internal/varlink/dev.nsbox.varlink",
    "internal/varlinkhost/varlinkhost.go",
  ]

  outputs = ["$go_target_dir/{{source}}"]

  deps = [":vendor_symlink", ":host_paths"]
}

go_binary("varlink-go-interface-generator") {
  package = "github.com/varlink/go/cmd/varlink-go-interface-generator"
  deps = [":gofiles"]
}

action("nsbox-varlink-interface") {
  script = "//build/bin_proxy.py"
  sources = get_target_outputs(":varlink-go-interface-generator")
  sources += ["$go_target_dir/internal/varlink/dev.nsbox.varlink"]
  outputs = ["$go_target_dir/internal/varlink/devnsbox.go"]
  args = [rebase_path(go_target_dir, root_build_dir)] + rebase_path(sources, go_target_dir)
  deps = [":gofiles", ":varlink-go-interface-generator"]
}

go_deps = [":gofiles", ":nsbox-varlink-interface"]

go_binary(product_name) {
  package = "github.com/refi64/nsbox/cmd/nsbox"
  deps = [":gofiles", ":nsbox-varlink-interface"]
}

go_binary("nsboxd") {
  package = "github.com/refi64/nsbox/cmd/nsboxd"
  deps = go_deps
}

go_binary("nsbox-invoker") {
  package = "github.com/refi64/nsbox/cmd/nsbox-invoker"
  deps = go_deps
}

go_binary("nsbox-host") {
  package = "github.com/refi64/nsbox/cmd/nsbox-host"
  deps = go_deps
  static = true
}

python_binary("$product_name-bender") {
  source = "//utils/nsbox-bender.py"
  output = "$install_dir/$bin_dir/$target_name"
  python_files_dir = "$install_dir/$share_dir/$product_name/python"
}

install_files("install_bin") {
  targets = [":$product_name"]
  deps = [":$product_name-bender"]
  output = "$bin_dir/{{source_file_part}}"
}

if (!is_stable_build) {
  symlink("install_bin_edge_alias") {
    name = "$install_dir/$bin_dir/nsbox"
    value = "./nsbox-edge"
    deps = [":nsbox-edge"]
  }

  symlink("install_bin_edge_bender_alias") {
    name = "$install_dir/$bin_dir/nsbox-bender"
    value = "./nsbox-edge-bender"
    deps = [":nsbox-edge-bender"]
  }
}

install_files("install_etc") {
  output = "$config_dir/profile.d/$product_name.sh"
  targets = [":nsbox_profile"]
}

install_files("install_polkit_actions") {
  output = "$share_dir/polkit-1/actions/$rdns_name.policy"
  targets = [":nsbox_policy"]
}

install_files("install_polkit_rules") {
  output = "$share_dir/polkit-1/rules.d/$rdns_name.rules"
  targets = [":nsbox_polkit_rules"]
}

install_files("install_private_exec") {
  targets = [":nsboxd", ":nsbox-invoker", ":nsbox-host"]
  output = "$libexec_dir/$product_name/{{source_file_part}}"
}

install_files("install_share_data") {
  sources = [
    "data/getty-override.conf",
    "data/nsbox-container.target",
    "data/nsbox-init.service",
    "data/scripts/nsbox-apply-env.sh",
    "data/scripts/nsbox-enter-run.sh",
    "data/scripts/nsbox-enter-setup.sh",
    "data/scripts/nsbox-init.sh",
  ]
  output = "$share_dir/$product_name/{{source}}"
}

install_files("install_share_images") {
  sources = [
    "images/arch/Dockerfile",
    "images/arch/metadata.json",
    "images/arch/playbook.yaml",
    "images/arch/roles/main/tasks/main.yaml",
    "images/debian/Dockerfile",
    "images/debian/metadata.json",
    "images/debian/playbook.yaml",
    "images/debian/roles/main/tasks/main.yaml",
    "images/fedora/metadata.json",
    "images/fedora/playbook.yaml",
    "images/fedora/roles/main/tasks/main.yaml",
    "images/fedora/roles/main/templates/nsbox.repo",
  ]
  output = "$share_dir/$product_name/{{source}}"
}

install_files("install_share_release") {
  targets = [":release_files"]
  output = "$share_dir/$product_name/release/{{source_file_part}}"
}

group("install") {
  deps = [
    ":install_bin",
    ":install_etc",
    ":install_polkit_actions",
    ":install_polkit_rules",
    ":install_private_exec",
    ":install_share_data",
    ":install_share_images",
    ":install_share_release",
  ]

  if (!is_stable_build) {
    deps += [
      ":install_bin_edge_alias",
      ":install_bin_edge_bender_alias",
    ]
  }
}

group("default") {
  deps = [":install"]
}

if (fedora_package) {
  group("fedora") {
    deps = ["//packaging/fedora"]
  }
}

if (fedora_guest_tools) {
  group("fedora_guest_tools") {
    deps = ["//guest-tools/fedora"]
  }
}

if (arch_guest_tools) {
  group("arch_guest_tools") {
    deps = ["//guest-tools/arch"]
  }
}

group("images") {
  deps = ["//images"]
}
